#!/usr/bin/env perl
use strict;
use warnings;

use VRPipe::FrontEnd;

my $frontend = VRPipe::FrontEnd->new(description => "Provide an overview of problems with pipelines that need user intervention to resolve.",
                                     opt_spec => [ [ 'setup=s', 'Show details for this PipelineSetup only (by name or id)', { persistent_object => 'PipelineSetup' } ],
                                                   [ 'retries=i', 'The number of retries the dispatch daemon is set to attempt before giving up (default 3)', { default => 3 } ] ]);

my $retries = $frontend->opts('retries');
my $setup = $frontend->get_pipelinesetups();

my $schema = $frontend->schema;

my @subs;
my $rs = $schema->resultset("Submission")->search({ "_failed" => 1, retries => $retries,
                                                    $setup ? ('stepstate.pipelinesetup' => $setup->id) : () },
                                                  $setup ? { join => 'stepstate' } : () );
while (my $sub = $rs->next) {
    push(@subs, $sub);
}

my %summary;
foreach my $sub (@subs) {
    my $state = $sub->stepstate;
    my $setup = $state->pipelinesetup;
    my $setup_name = $setup->name;
    
    my $stepm = $state->stepmember;
    my $step_name = $stepm->step->name;
    
    $summary{$setup->id.'|[]|'.$setup_name}->{$stepm->step_number.'|[]|'.$step_name}++;
}

my $key_sort = sub { my ($ay) = split('\|\[\]\|', $a); my ($be) = split('\|\[\]\|', $b); $ay <=> $be  };
if (keys %summary) {
    foreach my $setup (sort $key_sort keys %summary) {
        my ($setup_id, $setup_name) = split('\|\[\]\|', $setup);
        $frontend->output("---\nPipeline Setup '$setup_name' (id $setup_id) has the following problems:");
        
        my $steps = $summary{$setup};
        foreach my $step (sort $key_sort keys %{$steps}) {
            my ($step_id, $step_name) = split('\|\[\]\|', $step);
            my $fails = $steps->{$step};
            $frontend->output("\t", "Step $step_name has failed for $fails Data Elements; investigate by running: vrpipe-submissions --setup $setup_id --step '$step_name' --failed");
        }
        
        $frontend->output("---\n\n");
    }
}
else {
    if ($setup) {
        $frontend->output("There are currently no problems with setup ", $setup->id, " requiring manual intervention");
    }
    else {
        $frontend->output("There are currently no problems with anyone's pipelines requiring manual intervention");
    }
}

exit;